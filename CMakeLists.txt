#
# rpi0-cross-compile
#

CMAKE_MINIMUM_REQUIRED(VERSION 3.21)
PROJECT(rpi0-cross-compile C)

INCLUDE(CheckIncludeFile)
INCLUDE(CTest)

SET(GREETING_MSG "Hello, World!" CACHE STRING "Greeting message")

CHECK_INCLUDE_FILE(stdio.h HAVE_STDIO_H)
CONFIGURE_FILE(config.h.in include/config.h ESCAPE_QUOTES)

ADD_EXECUTABLE(rpi01 main.c)
ADD_EXECUTABLE(hello-world ALIAS rpi01)
TARGET_INCLUDE_DIRECTORIES(rpi01 PRIVATE ${CMAKE_CURRENT_BINARY_DIR}/include)
TARGET_LINK_OPTIONS(rpi01 PRIVATE -Wl,--trace)
IF(CMAKE_CROSSCOMPILING)
    ADD_CUSTOM_COMMAND(TARGET rpi01 POST_BUILD
        COMMAND ${CMAKE_COMMAND}
        -DCMAKE_READELF=${CMAKE_READELF}
        -P ${CMAKE_SOURCE_DIR}/cmake/arch-specific.cmake
        $<TARGET_OBJECTS:rpi01>
        $<TARGET_FILE:rpi01>
    )
ENDIF()

IF(BUILD_TESTING)
    ADD_TEST(NAME rpi01.execute COMMAND rpi01)
    SET_TESTS_PROPERTIES(rpi01.execute PROPERTIES
        PASS_REGULAR_EXPRESSION Hello
        TIMEOUT 15
        LABELS EXE
    )
ENDIF()
